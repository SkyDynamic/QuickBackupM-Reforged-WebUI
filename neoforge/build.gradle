plugins {
    id 'com.github.johnrengelman.shadow'
}

architectury {
    platformSetupLoomIde()
    neoForge()
}

configurations {
    common {
        canBeResolved = true
        canBeConsumed = false
    }
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentNeoForge.extendsFrom common

    // Files in this configuration will be bundled into your mod using the Shadow plugin.
    // Don't use the `shadow` configuration from the plugin itself as it's meant for excluding files.
    shadowBundle {
        canBeResolved = true
        canBeConsumed = false
    }
}

repositories {
    maven {
        name = 'NeoForged'
        url = 'https://maven.neoforged.net/releases'
    }
}

dependencies {
    neoForge "net.neoforged:neoforge:$rootProject.neoforge_version"

    common(project(path: ':common', configuration: 'namedElements')) { transitive false }
    shadowBundle project(path: ':common', configuration: 'transformProductionNeoForge')

    include(implementation 'org.glassfish.jersey.containers:jersey-container-grizzly2-http:3.1.10')
    include(implementation 'org.glassfish.jersey.core:jersey-server:3.1.10')
    include(implementation 'org.glassfish.jersey.media:jersey-media-json-jackson:3.1.10')
    include(implementation 'org.glassfish.jersey.inject:jersey-hk2:3.1.10')

    include(runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.13.0')
    include(runtimeOnly 'io.jsonwebtoken:jjwt-gson:0.13.0')
}

processResources {
    inputs.property 'version', project.version

    filesMatching('META-INF/neoforge.mods.toml') {
        expand version: project.version
    }
}

shadowJar {
    configurations = [project.configurations.shadowBundle]
    archiveClassifier = 'dev-shadow'
}

remapJar {
    input.set shadowJar.archiveFile
}
